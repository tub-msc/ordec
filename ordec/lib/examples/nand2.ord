# -*- version: ord2 -*-
from ordec.core import *

# Libary imports
from ordec.lib import Nmos, Pmos
from ordec.lib import Gnd, Vdc
# Simulation imports
from ordec.sim import HighlevelSim

cell Nand2:
    viewgen symbol -> Symbol:
        output y: .align=Orientation.East
        input a: .align=Orientation.West
        input b: .align=Orientation.West
        inout vdd: .align=Orientation.North
        inout vss: .align=Orientation.South
    viewgen schematic -> Schematic:
        net x # Between n1 and n2
        port y: .align=Orientation.West; .pos=(17,13)
        port a: .align=Orientation.East; .pos=(1,10)
        port b: .align=Orientation.East; .pos=(1,21)
        port vdd: .align=Orientation.East; .pos=(1,19)
        port vss: .align=Orientation.East; .pos=(1,0)

        Nmos n1: .pos=(4,1); .s -- vss; .d -- x; .g -- a
        Nmos n2: .pos=(4,7);  .s -- x; .d -- y; .g -- b
        Pmos p1: .pos=(4,14); .s -- vdd; .d -- y; .g -- a
        
        # Alternative way to connect instance:
        Pmos p2:
            .pos = (12,14)
            .s -- vdd
            .d -- y
            .g -- b

        # Connections can also be added after declaration of the instance:
        n1.b -- vss
        n2.b -- vss
        p1.b -- vdd
        p2.b -- vdd
        
        # Set width and length parameters for all transistors:
        for t in p1, p2, n1, n2:
            t.$w = 1u
            t.$l = 500n

cell Nand2Tb:
    viewgen symbol -> Symbol:
        pass
    viewgen schematic -> Schematic:
        net vdd
        net vss
        net a
        net b
        net y
        
        Nand2 DUT:
            .pos=(17,6)
            .vdd -- vdd
            .vss -- vss
            .a -- a
            .b -- b
            .y -- y
            
        Gnd gnd: .pos=(0,0); .p -- vss
        
        Vdc src_vdd: .pos=(0,6); .m -- vss; .p -- vdd; .$dc=5
        Vdc src_a: .pos=(10,6); .m -- vss; .p -- a; .$dc=5
        Vdc src_b: .pos=(5,6); .m -- vss; .p -- b; .$dc=0

    @generate
    def sim_dc(self):
        node = SimHierarchy.from_schematic(self.schematic)
        HighlevelSim(node).op()
        return node
